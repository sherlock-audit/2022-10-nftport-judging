GimelSec

high

# `ERC1155NFTProduct` does not support full functionality of `ERC1155`

## Summary

By design `ERC1155` is a combination of `ERC721` and `ERC20`. Its ability to supply > 1 token for a specific token index also allows support for fragmentation of NFTs. However, the current implementation of `ERC1155NFTProduct` disallows minting of a specific token index once it has been created. This handicaps the cross-chain NFT transfer functionality for fragmented NFTs.

## Vulnerability Detail

In `ERC1155NFTProduct`, both `mintByOwner` and `mintByOwnerBatch` checks the existence of target token id before minting. If token id already exists, minting is disallowed.

```solidity
    function mintByOwner(
        address account,
        uint256 id,
        uint256 amount,
        string memory tokenUri
    ) public onlyRole(MINT_ROLE) {
        require(!_exists(id), "NFT: token already minted");
        if (bytes(tokenUri).length > 0) {
            _tokenURIs[id] = tokenUri;
            emit URI(tokenUri, id);
        }
        _mint(account, id, amount, "");
        tokenSupply[id] += amount;
    }

    function mintByOwnerBatch(
        address[] memory to,
        uint256[] memory ids,
        uint256[] memory amounts,
        string[] memory uris
    ) public onlyRole(MINT_ROLE) {
        for (uint256 i = 0; i < ids.length; i++) {
            require(!_exists(ids[i]), "One of tokens is already minted");
            require(
                to[i] == address(to[i]),
                "NFT: one of addresses is invalid"
            );
            require(amounts[i] > 0, "NFT: all amounts must be > 0");
            tokenSupply[ids[i]] += amounts[i];
            if (bytes(uris[i]).length > 0) {
                _tokenURIs[ids[i]] = uris[i];
                emit URI(uris[i], ids[i]);
            }
            _mint(to[i], ids[i], amounts[i], "");
        }
    }
```

However, one of the core functionality of `ERC1155` is to combine the functionality of `ERC20` and `ERC721`, and allow > 1 copies of a token id. The current implementation of `ERC1155NFTProduct` would indicate that if a NFT is fragmented through `ERC1155`, and one copy is transferred through the `ERC1155NFTProduct` contract, all other copies with the same tokenid will no longer be transferable before the previous copy is burnt (setting exist to false).

## Impact

Current implementation greatly limits the functionality of `ERC1155` and may lead to unnecessary block of NFT transfer through nftPort. 

## Code Snippet


https://github.com/sherlock-audit/2022-10-nftport/blob/main/evm-minting-master/contracts/templates/ERC1155NFTProduct.sol#L265
https://github.com/sherlock-audit/2022-10-nftport/blob/main/evm-minting-master/contracts/templates/ERC1155NFTProduct.sol#L286

## Tool used

Manual Review

## Recommendation

If controlling the max amount of NFT minted is not required, directly removing the line below which checks the existence of ERC1155 id is sufficient.

```
        require(!_exists(id), "NFT: token already minted");
        require(!_exists(ids[i]), "One of tokens is already minted");
```

If it is desired that `ERC1155NFTProduct` helps in tracking and controlling the max mintable amount of a specific NFT id, consider introducing a `maxMintable` storage mapping to keep track and check against minted amounts.
