obront

medium

# Missing check for equal length arrays in transferByOwnerBatch and mintByOwnerBatch

## Summary

The `transferByOwnerBatch()` and `mintByOwnerBatch()` functions in the ERC1155 implementation does not check whether the lengths of the arrays submitted are equal. This can lead to unexpected results.

## Vulnerability Detail

In the `transferByOwnerBatch()` function, the user submits three arrays (addresses, ids, and amounts), while in the `mintByOwnerBatch()` function, the user submits four arrays (addresses, ids, amounts, uris).

The expectation is that the user submitting the function will ensure that the indexes of the arrays correspond to the correct values in the other arrays, and thus that the lengths will be the same.

Common practice in such a situation is to verify that the lengths are equal to ensure the user hasn't made an error. In other functions like `burnBatch()`, this verification is done by the underlying ERC1155 function. 

However, in these two functions, we simply iterate through the `ids` array without performing this check, and then call out to the underlying ERC1155 `_safeTransferFrom()`  or `_mint()` functions for each id separately.

## Impact

If the `ids` array is a shorter length than the other arrays, the additional values in the other arrays will be ignored. This could lead to transfers with unexpected results, which would be better served by reverting.

## Code Snippet

```
    function transferByOwnerBatch(
        address[] memory to,
        uint256[] memory ids,
        uint256[] memory amounts
    ) public onlyRole(TRANSFER_ROLE) {
        require(tokensTransferable, "Transfers are disabled");
        for (uint256 i = 0; i < ids.length; i++) {
            _safeTransferFrom(_owner, to[i], ids[i], amounts[i], "");
        }
    }
```

```
    function mintByOwnerBatch(
        address[] memory to,
        uint256[] memory ids,
        uint256[] memory amounts,
        string[] memory uris
    ) public onlyRole(MINT_ROLE) {
        for (uint256 i = 0; i < ids.length; i++) {
            require(!_exists(ids[i]), "One of tokens is already minted");
            require(
                to[i] == address(to[i]),
                "NFT: one of addresses is invalid"
            );
            require(amounts[i] > 0, "NFT: all amounts must be > 0");
            tokenSupply[ids[i]] += amounts[i];
            if (bytes(uris[i]).length > 0) {
                _tokenURIs[ids[i]] = uris[i];
                emit URI(uris[i], ids[i]);
            }
            _mint(to[i], ids[i], amounts[i], "");
        }
    }
```

## Tool used

Manual Review

## Recommendation

Add a check to the `transferByOwnerBatch()` function that confirms that to, ids, and amounts are all equal length.

```
require(ids.length == to.length, "mismatched array lengths");
require(ids.length == amounts.length, "mismatched array lengths");
```

Add a check to the `mintByOwnerBatch()` function that confirms that to, ids, amounts, and uris are all equal length.

```
require(ids.length == to.length, "mismatched array lengths");
require(ids.length == amounts.length, "mismatched array lengths");
require(ids.length == uris.length, "mismatched array lengths");
```